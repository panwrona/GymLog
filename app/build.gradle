apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

import org.ajoberstar.grgit.Grgit

def git = Grgit.open(currentDir: projectDir)
def commitsOnBranchCount = git.log(includes: ['HEAD']).size()
def commitTimestamp = git.head().time

android {
  compileSdkVersion 27
  buildToolsVersion "27.0.2"
  defaultConfig {
    applicationId "com.panwrona.gymlog"
    minSdkVersion 21
    targetSdkVersion 27
    versionCode commitTimestamp
    versionName "0.1.$commitsOnBranchCount"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    debug {
      keyAlias 'androiddebugkey'
      keyPassword 'android'
      storePassword 'android'
      storeFile file('../keystore/debug.keystore')
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.debug
    }

    qa {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".qa"
      versionNameSuffix "-qa"
      signingConfig signingConfigs.debug
    }

    develop {
      minifyEnabled true
      debuggable true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".develop"
      versionNameSuffix "-dev"
      signingConfig signingConfigs.debug
    }

    debug {
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
      signingConfig signingConfigs.debug
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

ext {
  android_support_version = '27.0.2'
  dagger_version = '2.11'
}

configurations.all {
  resolutionStrategy {
    force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  }
}

dependencies {

  //Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  //Android
  implementation "com.android.support:appcompat-v7:$android_support_version"
  implementation "com.android.support:design:$android_support_version"
  implementation "com.android.support:recyclerview-v7:$android_support_version"
  implementation "com.android.support:cardview-v7:$android_support_version"
  implementation "com.android.support:support-annotations:$android_support_version"

  //RxJava
  implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

  //Dependency Injection
  kapt "com.google.dagger:dagger-compiler:$dagger_version"
  implementation "com.google.dagger:dagger:$dagger_version"

  //Tests
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.12.0'
  testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

repositories {
  mavenCentral()
}
